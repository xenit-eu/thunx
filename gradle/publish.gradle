apply plugin: 'signing'

publishing {
    publications {
        all {
            pom {
                url = 'https://github.com/xenit-eu/thunx'
                name = project.name
                description = project.description

                scm {
                    connection = 'scm:git:git@github.com:xenit-eu/thunx.git'
                    developerConnection = 'scm:git:git@github.com:xenit-eu/thunx.git'
                    url = 'https://github.com/xenit-eu/thunx.git'
                }

                developers {
                    developer {
                        name = "XeniT"
                        organization = "XeniT Solutions NV"
                    }
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

pluginManager.withPlugin('java-library') {
    publishing {
        publications {
            library(MavenPublication) {
                from components.java

                versionMapping {
                    usage("java-api") {
                        fromResolutionResult()
                    }
                    usage("java-runtime") {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
}

pluginManager.withPlugin('java-platform') {
    publishing {
        publications {
            platform(MavenPublication) {
                from components.javaPlatform
            }
        }
    }
}