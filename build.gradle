apply from: 'gradle/jreleaser.gradle'

allprojects {
    pluginManager.withPlugin('java-library') {

        configurations {
            // See https://github.com/gradle/gradle/issues/10861
            // To avoid publishing the internal-platform
            internalPlatform {
                visible = false
                canBeConsumed = false
                canBeResolved = false
            }
            annotationProcessor.extendsFrom(internalPlatform)
            compileClasspath.extendsFrom(internalPlatform)
            runtimeClasspath.extendsFrom(internalPlatform)
            testAnnotationProcessor.extendsFrom(internalPlatform)
            testCompileClasspath.extendsFrom(internalPlatform)
            testRuntimeClasspath.extendsFrom(internalPlatform)
        }

        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(17)
            }
            withSourcesJar()
            withJavadocJar()
        }

        repositories {
            mavenCentral()
            maven {
                url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                content {
                    includeGroup 'com.contentgrid.opa-java-client'
                }
            }
        }
    }

    pluginManager.withPlugin('maven-publish') {
        apply from: "${rootDir}/gradle/publish.gradle"
    }
}
